plugins {
    id 'java'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.0.0-beta13'
}

group = 'net.voxelarc.allaychat'
version = '1.1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "voxelarc-snapshots"
        url = "https://repo.voxelarc.net/releases"
    }
    maven {
        url = "https://jitpack.io"
    }
    maven {
        name = "tcoded-releases"
        url = "https://repo.tcoded.com/releases"
    }
}

dependencies {
    compileOnly('org.projectlombok:lombok:1.18.30')
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")

    compileOnly("net.voxelarc.allaychat:api:1.1.0")
    compileOnly "com.tcoded:FoliaLib:0.5.1"

    compileOnly("com.openai:openai-java:2.12.0")

    compileOnly 'com.gitlab.ruany:LiteBansAPI:0.6.1'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId 'net.voxelarc.allaychat'
            artifactId 'aifilter'
        }
    }
    repositories {
        maven {
            name = "voxelarcSnapshots"
            url = uri("https://repo.voxelarc.net/snapshots")
            credentials {
                username = System.getenv("VOXELARC_REPO_USERNAME")
                password = System.getenv("VOXELARC_REPO_PASSWORD")
            }
        }
        maven {
            name = "voxelarcReleases"
            url = uri("https://repo.voxelarc.net/releases")
            credentials {
                username = System.getenv("VOXELARC_REPO_USERNAME")
                password = System.getenv("VOXELARC_REPO_PASSWORD")
            }
        }
    }
}

shadowJar {
    dependsOn(jar)

    archiveFileName = "${project.name}-${archiveVersion.get()}.jar"

    relocate 'com.tcoded.folialib', 'net.voxelarc.allaychat.libraries.folialib'

    from sourceSets.getByName("main").output
    configurations = [project.configurations.getByName("runtimeClasspath")]

}

build {
    dependsOn processResources
    dependsOn shadowJar
}